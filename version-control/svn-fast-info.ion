# Faster alternative to the current SVN plugin implementation.
#
# Works with svn 1.6, 1.7, 1.8.
# Use `svn_prompt_info` method to enquire the svn data.
# It's faster because his efficient use of svn (single svn call) which saves a lot on a huge codebase
# It displays the current status of the local files (added, deleted, modified, replaced, or else...)
#
# Use as a drop-in replacement of the svn plugin not as complementary plugin

fn svn_repo_need_upgrade repo:str -- Check if the repo needs upgrade
  grep -q "E155036" <<< $or($repo $(svn info ^> /dev/null))
  return $?
end

fn svn_current_branch_name repo:str -- Get the current branch name
  grep '^URL:' <<< $or($repo $(svn info ^> /dev/null)) | egrep -o '(tags|branches)/[^/]+|trunk'
end

fn svn_repo_root_name repo:str -- Check the root repo name
  grep '^Repository\ Root:' <<< $or($repo $(svn info ^> /dev/null)) | sed 's#.*/##'
end

fn svn_current_revision repo:str -- Check the current revision
  echo $or($repo $(svn info ^> /dev/null)) | sed -n 's/Revision: //p'
end

fn svn_status_info theme:hmap[str] -- get the svn status. Keys for theme: additions, deletions, modifications, replacements, untracked, dirty
  let svn_status_string = ""
  let svn_status = $(svn status ^> /dev/null)
  if /usr/bin/env grep -E '^\s*A' &> /dev/null <<< $svn_status
    let svn_status_string ::= $or(@theme['additinos'] '+')
  end
  if /usr/bin/env grep -E '^\s*D' &> /dev/null <<< $svn_status
    let svn_status_string ::= $or(@theme['deletions'] '✖')
  end
  if /usr/bin/env grep -E '^\s*M' &> /dev/null <<< $svn_status
    let svn_status_string ::= $or(@theme['modifications'] '✎')
  end
  if /usr/bin/env grep -E '^\s*[R~]' &> /dev/null <<< $svn_status
    let svn_status_string ::= $or(@theme['replacements'] '∿')
  end
  if /usr/bin/env grep -E '^\s*\?' &> /dev/null <<< $svn_status
    let svn_status_string ::= $or(@theme['untracked'] '?')
  end
  if /usr/bin/env grep -E '^\s*[CI!L]' &> /dev/null <<< $svn_status
    let svn_status_string ::= $or(@theme['dirty'] '!')
  end
  echo $svn_status_string
end

