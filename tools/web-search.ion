# web_search from terminal

# This plugin adds aliases for searching with Google, Wiki, Bing, YouTube and other popular services.

## Usage

# `<search engine> [<terms>...]`

# Also there are aliases for bang-searching DuckDuckGo:

# | Context   | Bang  |
# |-----------|-------|
# | `wiki`    | `!w`  |
# | `news`    | `!n`  |
# | `youtube` | `!yt` |
# | `map`     | `!m`  |
# | `image`   | `!i`  |
# | `ducky`   | `!`   |

fn web_search engine:str keywords:[str] -- Search keywords on the given search engine
  let urls:hmap[str] = [ \
    google        = "https://www.google.com/search?q=" \
    bing          = "https://www.bing.com/search?q=" \
    yahoo         = "https://search.yahoo.com/search?p=" \
    duckduckgo    = "https://www.duckduckgo.com/?q=" \
    startpage     = "https://www.startpage.com/do/search?q=" \
    yandex        = "https://yandex.ru/yandsearch?text=" \
    github        = "https://github.com/search?q=" \
    baidu         = "https://www.baidu.com/s?wd=" \
    ecosia        = "https://www.ecosia.org/search?q=" \
    goodreads     = "https://www.goodreads.com/search?q=" \
    qwant         = "https://www.qwant.com/?q=" \
    givero        = "https://www.givero.com/search?q=" \
    stackoverflow = "https://stackoverflow.com/search?q=" \
  ]

  # check whether the search engine is supported
  if not contains $engine @keys(@urls)
    echo "Search engine $engine not supported."
    return 1
  end

  # search or go to main page depending on number of arguments passed
  let url = @urls[$engine]
  if not eq $len(@keywords) 0
    # build search url:
    # join arguments passed with '+', then append to search engine URL
    let url ::= $join(@keywords '+')
  else
    # build main page url:
    # split by '/', then rejoin protocol (1) and domain (2) parts with '//'
    let url = $join(@split($url '/')[..3] '/')
  end

  # TODO: open_command like oh-my-zsh
  xdg-open $url
end


alias bing = 'web_search bing'
alias google = 'web_search google'
alias yahoo = 'web_search yahoo'
alias ddg = 'web_search duckduckgo'
alias sp = 'web_search startpage'
alias yandex = 'web_search yandex'
alias github = 'web_search github'
alias baidu = 'web_search baidu'
alias ecosia = 'web_search ecosia'
alias goodreads = 'web_search goodreads'
alias qwant = 'web_search qwant'
alias givero = 'web_search givero'
alias stackoverflow = 'web_search stackoverflow'

#add your own !bang searches here
alias wiki = 'web_search duckduckgo \!w'
alias news = 'web_search duckduckgo \!n'
alias youtube = 'web_search duckduckgo \!yt'
alias map = 'web_search duckduckgo \!m'
alias image = 'web_search duckduckgo \!i'
alias ducky = 'web_search duckduckgo \!'
